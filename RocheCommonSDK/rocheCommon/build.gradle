import com.roche.ssg.buildsrc.*

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

apply from: "$project.rootDir/scripts/kotlin.gradle"
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

android {
    compileSdkVersion ConfigData.compileSdkVersion
    buildToolsVersion ConfigData.buildToolsVersion

    defaultConfig {
        minSdkVersion ConfigData.minSdkVersion
        targetSdkVersion ConfigData.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    kotlinOptions {
        jvmTarget = '1.6'
    }
    dataBinding {
        enabled = true
    }
    viewBinding {
        enabled = true
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'RocheCommonComponent' //put here your groupId
            artifactId project.getName() //put here your artifactId
            version android.defaultConfig.versionName //put here your library version

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
        }
    }
}

artifactory {
    contextUrl = 'https://dhs.jfrog.io/dhs/'
    publish {
        repository {
            repoKey = getProperty('artifactory.repokey')
            username = getProperty('artifactory.user')
            password = getProperty('artifactory.password')
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
        }
    }
}
dependencies {

    implementation Deps.kotlin
    implementation Deps.AndroidX.coreKtx
    implementation Deps.AndroidX.appCompat
    implementation Deps.materialDesign
    testImplementation Deps.junit
    androidTestImplementation Deps.AndroidX.Test.testExt
    androidTestImplementation Deps.AndroidX.Test.espressoCore
    implementation Deps.AndroidX.localBroadcastManager
}